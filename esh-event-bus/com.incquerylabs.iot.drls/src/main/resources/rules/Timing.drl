package homeioexample

declare SunriseOnce
    @role( event )
end


declare SunriseDuration
    @role( event )
end


rule "Switch off lights at sunrise"
    when
        SunriseOnce()
        
        Item(name == "E_Lights_Analog", state != OFF )
    then
        System.out.println( "IncQuery droolsbundle: " + drools.getRule().getName());
        openhab.postCommand("E_Lights_Analog", OFF);
end


rule "Sunrise event start"
    when
        not( SunriseDuration() )
        DateTime( name == "HomeIO_Date", $homeioHours : hoursOfDay, $homeioMinutes : minutesOfHour)
        DateTime( name == "Sunrise_Time", $sunriseHours : hoursOfDay, $sunriseMinutes : minutesOfHour)
        
        eval ( $homeioHours == $sunriseHours && $homeioMinutes >= $sunriseMinutes && $homeioMinutes <= $sunriseMinutes + 15 )
    then
        System.out.println( "IncQuery droolsbundle: " + drools.getRule().getName());
        insert(new SunriseDuration());
        insert(new SunriseOnce());
end
       
       
rule "Sunrise once event end"
        salience -100
    when
        $sunriseOnce : SunriseOnce()
    then
        retract($sunriseOnce);

end


rule "Sunrise duration event end"
        salience -100
    when
        $sunriseDuration : SunriseDuration()
        
        DateTime( name == "HomeIO_Date", $homeioHours : hoursOfDay, $homeioMinutes : minutesOfHour)
        DateTime( name == "Sunrise_Time", $sunriseHours : hoursOfDay, $sunriseMinutes : minutesOfHour)
        
        eval ( !( $homeioHours == $sunriseHours && $homeioMinutes >= $sunriseMinutes && $homeioMinutes <= $sunriseMinutes + 15) )
    then
        System.out.println( "IncQuery droolsbundle: " + drools.getRule().getName());
        retract($sunriseDuration);
end
       
